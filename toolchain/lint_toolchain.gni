import("../config/colors.gni")

# This template defines a toolchain for lint.
#
# It requires the following variables specifying the executables to run:
#  - lintcc
#  - lintcxx
# and a toolchain_args scope variable with the following variables:
#  - current_cpu
#  - current_os
#  - current_platform
#
# Optional parameters that control the tools:
#
#  - extra_asmflags
#      Extra flags to be appended when assembling files.
#  - extra_asmppflags
#      Extra flags to be appended when assembling files. "ASMPP" stands for
#      "ASM PreProcessor" in this context, although it can be used for non-
#      preprocessor flags as well.
#  - extra_cflags
#      Extra flags to be appended when compiling C files (but not C++ files).
#  - extra_cppflags
#      Extra flags to be appended when compiling both C and C++ files. "CPP"
#      stands for "C PreProcessor" in this context, although it can be
#      used for non-preprocessor flags as well. Not to be confused with
#      "CXX" (which follows).
#  - extra_cxxflags
#      Extra flags to be appended when compiling C++ files (but not C files).
#  - extra_objcflags
#      Extra flags to be appended when compiling Objective C files.
#  - extra_objcxxflags
#      Extra flags to be appended when compiling Objective C++ files.
#
#  - lintasm
#      The lint executable to run for assembly source files.
#  - deps
#      Just forwarded to the toolchain definition.
#  - executable_extension
#      If this string is specified it will be used for the file extension
#      for an executable, rather than using no extension; targets will
#      still be able to override the extension using the output_extension
#      variable.
#  - loadable_module_extension
#      If this string is specified it will be used for the file extension
#      for a loadable module, rather than the shlib_extension.
#  - rebuild_define
#      The contents of this string, if specified, will be passed as a #define
#      to the toolchain. It can be used to force recompiles whenever a
#      toolchain is updated.
#  - shlib_extension
#      If this string is specified it will be used for the file extension
#      for a shared library, rather than default value specified in
#      toolchain_defaults.gni
#
template("lint_toolchain") {
  assert(defined(invoker.lintcc), "${color_error}lint_toolchain() must specify a \"lintcc\" value${color_none}")
  assert(defined(invoker.lintcxx), "${color_error}lint_toolchain() must specify a \"lintcxx\" value${color_none}")

  # GN's syntax can't handle more than one scope dereference at once, like
  # "invoker.toolchain_args.foo", so make a temporary to hold the toolchain
  # args so we can do "invoker_toolchain_args.foo".
  assert(defined(invoker.toolchain_args),
         "${color_error}Toolchains must specify toolchain_args")
  invoker_toolchain_args = invoker.toolchain_args
  assert(defined(invoker_toolchain_args.current_cpu),
         "${color_error}toolchain_args must specify a \"current_cpu\"${color_none}")
  assert(defined(invoker_toolchain_args.current_os),
         "${color_error}toolchain_args must specify a \"current_os\"${color_none}")
  assert(defined(invoker_toolchain_args.current_platform),
         "${color_error}toolchain_args must specify a \"current_platform\"${color_none}")

  import("$bam_core_path/toolchain/toolchain_defaults.gni")

  # Note: OS definitions do not work for toolchains,
  # so we must redefine them here
  _is_win = (invoker_toolchain_args.current_os == "win")
  mark_used(_is_win)

  toolchain(target_name) {
    # This define changes when the toolchain changes, forcing a rebuild.
    # Nothing should ever use this define.
    if (defined(invoker.rebuild_define)) {
      rebuild_string = "-D" + invoker.rebuild_define + " "
    } else {
      rebuild_string = ""
    }

    lintcc  = invoker.lintcc
    lintcxx = invoker.lintcxx
    if (defined(invoker.lintasm)) {
      lintasm = invoker.lintasm
    } else {
      lintasm = ""
    }

    if (defined(invoker.shlib_extension)) {
      default_shlib_extension = invoker.shlib_extension
    } else if (_is_win) {
      default_shlib_extension = ".dll"
    } else {
      default_shlib_extension = shlib_extension
    }

    if (defined(invoker.executable_extension)) {
      default_executable_extension = invoker.executable_extension
    } else {
      default_executable_extension = ".lint"
    }

    if (defined(invoker.extra_asmflags) && invoker.extra_asmflags != "") {
      extra_asmflags = " " + invoker.extra_asmflags
    } else {
      extra_asmflags = ""
    }

    if (defined(invoker.extra_asmppflags) && invoker.extra_asmppflags != "") {
      extra_asmppflags = " " + invoker.extra_asmppflags
    } else {
      extra_asmppflags = ""
    }

    if (defined(invoker.extra_cflags) && invoker.extra_cflags != "") {
      extra_cflags = " " + invoker.extra_cflags
    } else {
      extra_cflags = ""
    }

    if (defined(invoker.extra_cppflags) && invoker.extra_cppflags != "") {
      extra_cppflags = " " + invoker.extra_cppflags
    } else {
      extra_cppflags = ""
    }

    if (defined(invoker.extra_cxxflags) && invoker.extra_cxxflags != "") {
      extra_cxxflags = " " + invoker.extra_cxxflags
    } else {
      extra_cxxflags = ""
    }

    if (defined(invoker.extra_objcflags) && invoker.extra_objcflags != "") {
      extra_objcflags = " " + invoker.extra_objcflags
    } else {
      extra_objcflags = ""
    }

    if (defined(invoker.extra_objcxxflags) && invoker.extra_objcxxflags != "") {
      extra_objcxxflags = " " + invoker.extra_objcxxflags
    } else {
      extra_objcxxflags = ""
    }

    sys_include_switch = "--i"

    tool("cc") {
      if (defined(invoker.source_extensions_cc)) {
        source_extensions = invoker.source_extensions_cc
      }
      if (defined(invoker.depfile)) {
        depfile = invoker.depfile
        depsformat = invoker.depsformat
      }
      if (defined(invoker.pre_lintcc)) {
        command = invoker.pre_lintcc + " && "
      } else {
        command = ""
      }
      command += "$lintcc ${rebuild_string}{{defines}} {{include_dirs}} {{sys_include_dirs}}${extra_cppflags}${extra_cflags} {{cppflags}} {{cppflags_c}} {{source}}"
      command += " && touch {{output}}"
      if (defined(invoker.post_lintcc)) {
        command += " && " + invoker.post_lintcc
      }
      description = "LINT CC {{output}}"
      outputs = [
        "{{source_out_dir}}/{{source_file_part}}.lint",
      ]
    }

    tool("cxx") {
      if (defined(invoker.source_extensions_cxx)) {
        source_extensions = invoker.source_extensions_cxx
      }
      if (defined(invoker.depfile)) {
        depfile = invoker.depfile
        depsformat = invoker.depsformat
      }
      if (defined(invoker.pre_lintcxx)) {
        command = invoker.pre_lintcxx + " && "
      } else {
        command = ""
      }
      command += "$lintcxx ${rebuild_string}{{defines}} {{include_dirs}} {{sys_include_dirs}}${extra_cppflags}${extra_cxxflags} {{cppflags}} {{cppflags_cc}} {{source}}"
      command += " && touch {{output}}"
      if (defined(invoker.post_lintcxx)) {
        command += " && " + invoker.post_lintcxx
      }
      description = "LINT CXX {{output}}"
      outputs = [
        "{{source_out_dir}}/{{source_file_part}}.lint",
      ]
    }

    tool("objc") {
      if (defined(invoker.source_extensions_objc)) {
        source_extensions = invoker.source_extensions_objc
      }
      if (defined(invoker.depfile)) {
        depfile = invoker.depfile
        depsformat = invoker.depsformat
      }
      if (defined(invoker.pre_lintcc)) {
        command = invoker.pre_lintcc + " && "
      } else {
        command = ""
      }
      command += "$lintcc ${rebuild_string}{{defines}} {{include_dirs}} {{sys_include_dirs}}${extra_cppflags}${extra_objcflags} {{cppflags}} {{cppflags_objc}} {{source}}"
      command += " && touch {{output}}"
      if (defined(invoker.post_lintcc)) {
        command += " && " + invoker.post_lintcc
      }
      description = "LINT OBJC {{output}}"
      outputs = [
        "{{source_out_dir}}/{{source_file_part}}.lint",
      ]
    }

    tool("objcxx") {
      if (defined(invoker.source_extensions_objcxx)) {
        source_extensions = invoker.source_extensions_objcxx
      }
      if (defined(invoker.depfile)) {
        depfile = invoker.depfile
        depsformat = invoker.depsformat
      }
      if (defined(invoker.pre_lintcxx)) {
        command = invoker.pre_lintcxx + " && "
      } else {
        command = ""
      }
      command += "$lintcxx ${rebuild_string}{{defines}} {{include_dirs}} {{sys_include_dirs}}${extra_cppflags}${extra_objcxxflags} {{cppflags}} {{cppflags_objcc}} {{source}}"
      command += " && touch {{output}}"
      if (defined(invoker.post_lintcxx)) {
        command += " && " + invoker.post_lintcxx
      }
      description = "LINT OBJCXX {{output}}"
      outputs = [
        "{{source_out_dir}}/{{source_file_part}}.lint",
      ]
    }

    tool("asm") {
      if (defined(invoker.source_extensions_asm)) {
        source_extensions = invoker.source_extensions_asm
      }
      if (lintasm == "") {
        # Disable lint for assembly files
        source_extensions = []
        source_extensions = [ "." ]
      }
      if (defined(invoker.depfile)) {
        depfile = invoker.depfile
        depsformat = invoker.depsformat
      }
      if (defined(invoker.pre_lintasm)) {
        command = invoker.pre_lintasm + " && "
      } else {
        command = ""
      }
      command += "$lintasm ${rebuild_string}{{defines}} {{include_dirs}} {{sys_include_dirs}}${extra_asmppflags}${extra_asmflags} {{asmppflags}} {{source}}"
      command += " && touch {{output}}"
      if (defined(invoker.post_lintasm)) {
        command += " && " + invoker.post_lintasm
      }
      description = "LINT ASM {{output}}"
      outputs = [
        "{{source_out_dir}}/{{source_file_part}}.lint",
      ]
    }

    tool("alink") {
      command = "touch {{output}}"
      description = "LINT AR {{output}}"
      outputs = [
        "{{output_dir}}/{{target_output_name}}{{output_extension}}",
      ]
      default_output_dir = "{{target_out_dir}}"
      default_output_extension = ".a.lint"
      output_prefix = "lib"
    }

    tool("solink") {
      soname = "{{target_output_name}}{{output_extension}}"
      sofile = "{{output_dir}}/$soname"

      command = "touch {{output}}"

      description = "LINT SOLINK $sofile"

      # Use this for {{output_extension}} expansions unless a target manually
      # overrides it (in which case {{output_extension}} will be what the target
      # specifies).
      default_output_extension = default_shlib_extension + ".lint"

      default_output_dir = root_shlib_dir

      output_prefix = "lib"

      outputs = [
        sofile,
      ]
    }

    tool("solink_module") {
      soname = "{{target_output_name}}{{output_extension}}"
      sofile = "{{output_dir}}/$soname"

      command = "touch {{output}}"

      description = "LINT SOLINK_MODULE $sofile"

      # Use this for {{output_extension}} expansions unless a target manually
      # overrides it (in which case {{output_extension}} will be what the target
      # specifies).
      if (defined(invoker.loadable_module_extension)) {
        default_output_extension = invoker.loadable_module_extension + ".lint"
      } else {
        default_output_extension = default_shlib_extension + ".lint"
      }

      default_output_dir = root_shlib_dir

      output_prefix = "lib"

      outputs = [
        sofile,
      ]
    }

    tool("link") {
      exename = "{{target_output_name}}{{output_extension}}"
      outfile = "{{output_dir}}/$exename"

      # Use this for {{output_extension}} expansions unless a target manually
      # overrides it (in which case {{output_extension}} will be what the target
      # specifies).
      default_output_extension = default_executable_extension

      default_output_dir = "{{root_out_dir}}"

      command = "touch {{output}}"
      description = "LINT LINK $outfile"
      outputs = [
        outfile,
      ]
    }

    tool("stamp") {
      command = "touch {{output}}"
      description = "STAMP {{output}}"
    }

    tool("copy") {
      command = "ln -f {{source}} {{output}} 2>/dev/null || (rm -rf {{output}} && cp -af {{source}} {{output}})"
      description = "COPY {{source}} {{output}}"
    }

    # When invoking this toolchain not as the default one, these args will be
    # passed to the build. They are ignored when this is the default toolchain.
    toolchain_args = {
      if (defined(invoker.pkg_config)) {
        pkg_config = invoker.pkg_config
      }

      forward_variables_from(invoker_toolchain_args, "*", [], true)

      target_os = current_os
      target_cpu = current_cpu
      target_platform = current_platform
    }

    forward_variables_from(invoker, [ "deps" ])
  }
}
